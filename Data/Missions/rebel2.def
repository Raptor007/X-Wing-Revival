mission_name Rebel 2: Requiem
mission_desc Defend the medical frigate until evacuation is complete.
gametype hunt
player_team rebel
player_ship X/W
player_group 3
defending_team rebel
rebel_flagship FRG
rebel_flagship_name Redemption
rebel_flagship_shields 0.2
rebel_bomber X/W
rebel_fighter_squadron Green
rebel_fighter_group 3
empire_fighter_squadron Delta
rebel_waves 1
empire_waves 1
ai_empire_ratio 1.25
ai_fighter_ratio 1.5
player_spawn 4000 0 0
rebel_spawn 3800 0 0
empire_spawn -7000 0 -100
phase 1

at 0:01
spawn rebel SHU "Medevac 1" at 2900 250 50
wait 0.6
spawn rebel SHU "Medevac 2" at 2700 150 70
wait 0.4
spawn rebel SHU "Medevac 3" at 2500 180 -100
wait 0.6
spawn rebel SHU "Medevac 4" at 2300 300 -50
wait 0.9
spawn rebel CRV "Korolev" at 1800 200 -10

on destroyed "Medevac"
wait 3
defeat

on destroyed "Korolev"
wait 3
defeat

at 0:10
spawn empire FRG "Warspite" at -4500 0 0 facing 0.1 0.9 0
wait 2
spawn empire T/B "Eta 1" at -4000 -100 0 facing 1 0 0
spawn empire T/B "Eta 2" at -4000 -100 100 facing 1 0 0
spawn empire T/B "Eta 3" at -4000 -100 -100 facing 1 0 0

at 0:40
jump "Warspite"

at 1:05
jump "Medevac 1"
wait 1.5
jump "Medevac 2"
wait 0.6
jump "Medevac 3"
wait 0.9
jump "Medevac 4"

at 1:09 if player alive
alert r2_transports.wav The transports are away!

at 1:15 if phase 1
phase 2

when ( phase 1 ) and ( no empire here ) and ( no players within 2K of "Redemption" )
phase 2

when phase 2
spawn empire FRG "Warspite" at 5500 0 0 facing -0.5 -0.8 0
spawn empire T/F "Alpha 1" at 5100 0 0 facing -1 0 0
spawn empire T/F "Alpha 2" at 5110 0 100 facing -1 0 0
spawn empire T/B "Gamma 1" at 5000 -100 0 facing -1 0 0
spawn empire T/B "Gamma 2" at 5020 -100 100 facing -1 0 0
spawn empire T/B "Gamma 3" at 5030 -100 -100 facing -1 0 0
wait 2
alert r2_begin.wav They're heading for the medical frigate!

on empire fighter destroyed if ( phase 2 ) and ( no empire fighters here ) and ( players alive )
wait 3
jump "Warspite"
wait 2
alert r2_secure.wav Alright, the area is secured!
wait 1.5
message Nice shooting.
phase 3

when ( phase 3 ) and ( "Redemption" alive ) and ( "Korolev" alive )
wait 2
victory

on victory
next_mission rebel3
wait 3
jump "Korolev"
wait 2
jump "Redemption"
wait 2
jump player ships

on defeat
wait 1.5
alert Silly/failed.wav You blew it, and you took me down with you!
wait 1.5
message I thought you had great reflexes!
wait 1.7
message What happened to you??
wait 0.8
message I used to have, like, a good pilot!
wait 1.5
message Man, you're slipping.
